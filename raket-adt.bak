;;            -----------
;; ----------  Raket ADT  ----------
;;            -----------


; constructor om raket aan te maken
; de raket begint altijd op een gegeven positie
(define (maak-raket positie)
  (let ((levens aantal-levens-raket))


  ; de beweeg! functie die de beweeg van positie gaat aanroepen
  ; om zo de raket naar links of rechts te laten bewegen afhankelijk van het richtingsargument
  (define (beweeg! richting)
    (if (not (rand-geraakt?))
        ((positie 'beweeg!) richting)
        (let ((x (positie 'x)))
                 ; linkerrand geraakt (dan mag je alleen naar rechts)
          (cond ((and (= x 0) (eq? richting 'rechts))
                 ((positie 'beweeg!) richting))
                 ; rechterrand geraakt (dan mag je alleen naar links)
                ((and (= x (- spel-breedte 1)) (eq? richting 'links))
                 ((positie 'beweeg!) richting))))))

  
  ; rand-geraakt? : / -> boolean
  (define (rand-geraakt?)
    ((positie 'rand-horizontaal?)))

  ; positie! : Positie -> /
  (define (positie! nieuwe_positie)
    (set! positie nieuwe_positie))

  ; schiet! operatie gaat een nieuwe kogel aanmaken op de positie van de raket
  (define (schiet!)
    (maak-kogel (maak-positie (positie 'x) (positie 'y))))

  ; verminder-levens! : / -> /
  (define (verminder-levens!)
    (set! levens (- levens 1)))

  ; voeg-leven-toe! : / -> /
  (define (voeg-leven-toe!)
    (set! levens (+ levens 1)))

  ; reset-levens! : /> /
  (define (reset-levens!)
    (set! levens aantal-levens-raket))

  ; de dispatch functie
  (define (dispatch-raket msg)
    (cond((eq? msg 'beweeg!) beweeg!)
         ((eq? msg 'positie) positie)
         ((eq? msg 'positie!) positie!)
         ((eq? msg 'schiet!) schiet!)
         ((eq? msg 'levens) levens)
         ((eq? msg 'verminder-levens!) verminder-levens!)
         ((eq? msg 'reset-levens!) reset-levens!)
         (else (display "ongeldige boodschap - raket"))))
  dispatch-raket))

  

  

  
  